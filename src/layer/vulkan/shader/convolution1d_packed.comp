// Copyright 2026 Tencent
// SPDX-License-Identifier: BSD-3-Clause

#version 450

#extension GL_GOOGLE_include_directive : enable
#include "vulkan_activation.comp"

layout(constant_id = 0) const int kernel_w = 1;
layout(constant_id = 1) const int dilation_w = 1;
layout(constant_id = 2) const int stride_w = 1;
layout(constant_id = 3) const int bias_term = 0;
layout(constant_id = 4) const int activation_type = 0;
layout(constant_id = 5) const float activation_param_0 = 0;
layout(constant_id = 6) const float activation_param_1 = 0;
layout(constant_id = 7) const int elempack = 1;
layout(constant_id = 8) const int out_elempack = 1;

#define shape_constant_id_offset 9
layout(constant_id = shape_constant_id_offset + 0) const int w = 0;
layout(constant_id = shape_constant_id_offset + 1) const int h = 0;

layout(constant_id = shape_constant_id_offset + 2) const int outw = 0;
layout(constant_id = shape_constant_id_offset + 3) const int outh = 0;

layout(constant_id = shape_constant_id_offset + 4) const int num_output = 0;

// scalar view (for pack1 input/output access)
layout(binding = 0) readonly buffer bottom_blob_1 { sfp bottom_blob_data_1[]; };
layout(binding = 1) writeonly buffer top_blob_1 { sfp top_blob_data_1[]; };

// vec4 view (for pack4 input/output access, weight/bias always vec4)
layout(binding = 2) readonly buffer bottom_blob_4 { sfpvec4 bottom_blob_data_4[]; };
layout(binding = 3) writeonly buffer top_blob_4 { sfpvec4 top_blob_data_4[]; };
layout(binding = 4) readonly buffer weight_blob { sfpvec4 weight_data[]; };
layout(binding = 5) readonly buffer bias_blob { sfpvec4 bias_data[]; };

layout(push_constant) uniform parameter
{
    int w;
    int h;

    int outw;
    int outh;

    int num_output;
} p;

void main()
{
    int gx = int(gl_GlobalInvocationID.x) * 2;
    int gy = int(gl_GlobalInvocationID.y) * 2;

    if (gx >= psc(outw) || gy >= psc(outh))
        return;

    const ivec2 gx2 = gx + ivec2(0, 1);
    const ivec2 gy2 = gy + ivec2(0, 1);

    afpvec4 sum0;
    afpvec4 sum1;
    afpvec4 sum2;
    afpvec4 sum3;

    if (bias_term == 1)
    {
        sum0 = buffer_ld4(bias_data, gy2.x);
        sum2 = buffer_ld4(bias_data, gy2.y);
        sum1 = sum0;
        sum3 = sum2;
    }
    else
    {
        sum0 = afpvec4(0.0f);
        sum1 = afpvec4(0.0f);
        sum2 = afpvec4(0.0f);
        sum3 = afpvec4(0.0f);
    }

    ivec2 v_offset = gx2 * stride_w;
    ivec2 w_offset = gy2 * psc(h) * kernel_w;

    for (int y = 0; y < psc(h); y++)
    {
        for (int x = 0; x < kernel_w; x++)
        {
            if (elempack == 4)
            {
                afpvec4 v0 = buffer_ld4(bottom_blob_data_4, v_offset.x + x * dilation_w);
                afpvec4 v1 = buffer_ld4(bottom_blob_data_4, v_offset.y + x * dilation_w);

                // weight is 4x4 mat, stored as 4 consecutive vec4
                afpmat4 k0 = afpmat4(
                    buffer_ld4(weight_data, (w_offset.x + x) * 4 + 0),
                    buffer_ld4(weight_data, (w_offset.x + x) * 4 + 1),
                    buffer_ld4(weight_data, (w_offset.x + x) * 4 + 2),
                    buffer_ld4(weight_data, (w_offset.x + x) * 4 + 3));
                afpmat4 k1 = afpmat4(
                    buffer_ld4(weight_data, (w_offset.y + x) * 4 + 0),
                    buffer_ld4(weight_data, (w_offset.y + x) * 4 + 1),
                    buffer_ld4(weight_data, (w_offset.y + x) * 4 + 2),
                    buffer_ld4(weight_data, (w_offset.y + x) * 4 + 3));

                sum0 += v0 * k0;
                sum1 += v1 * k0;
                sum2 += v0 * k1;
                sum3 += v1 * k1;
            }
            else // elempack == 1
            {
                afp v0 = buffer_ld1(bottom_blob_data_1, v_offset.x + x * dilation_w);
                afp v1 = buffer_ld1(bottom_blob_data_1, v_offset.y + x * dilation_w);

                // weight is vec4 (4 output channels per input channel)
                afpvec4 k0 = buffer_ld4(weight_data, w_offset.x + x);
                afpvec4 k1 = buffer_ld4(weight_data, w_offset.y + x);

                sum0 += v0 * k0;
                sum1 += v1 * k0;
                sum2 += v0 * k1;
                sum3 += v1 * k1;
            }
        }
        v_offset += psc(w);
        w_offset += kernel_w;
    }

    sum0 = activation_afpvec4(sum0, activation_type, activation_param_0, activation_param_1);
    sum1 = activation_afpvec4(sum1, activation_type, activation_param_0, activation_param_1);
    sum2 = activation_afpvec4(sum2, activation_type, activation_param_0, activation_param_1);
    sum3 = activation_afpvec4(sum3, activation_type, activation_param_0, activation_param_1);

    if (out_elempack == 4)
    {
        const int gi = gy * psc(outw) + gx;

        buffer_st4(top_blob_data_4, gi, sum0);
        if (gx + 1 < psc(outw)) buffer_st4(top_blob_data_4, gi + 1, sum1);
        if (gy + 1 < psc(outh)) buffer_st4(top_blob_data_4, gi + psc(outw), sum2);
        if (gy + 1 < psc(outh) && gx + 1 < psc(outw)) buffer_st4(top_blob_data_4, gi + psc(outw) + 1, sum3);
    }
    else // out_elempack == 1
    {
        // outh is num_output_packed/4, outcstep is outw * 4 scalar channels
        const int outcstep = psc(outw);
        const int gi = gy * outcstep * 4 + gx;

        const ivec2 base_ch = gy2 * 4;
        const int nout = psc(num_output);

        buffer_st1(top_blob_data_1, gi, sum0.r);
        if (base_ch.x + 1 < nout) buffer_st1(top_blob_data_1, gi + outcstep, sum0.g);
        if (base_ch.x + 2 < nout) buffer_st1(top_blob_data_1, gi + outcstep * 2, sum0.b);
        if (base_ch.x + 3 < nout) buffer_st1(top_blob_data_1, gi + outcstep * 3, sum0.a);

        if (gx + 1 < psc(outw))
        {
            buffer_st1(top_blob_data_1, gi + 1, sum1.r);
            if (base_ch.x + 1 < nout) buffer_st1(top_blob_data_1, gi + 1 + outcstep, sum1.g);
            if (base_ch.x + 2 < nout) buffer_st1(top_blob_data_1, gi + 1 + outcstep * 2, sum1.b);
            if (base_ch.x + 3 < nout) buffer_st1(top_blob_data_1, gi + 1 + outcstep * 3, sum1.a);
        }
        if (gy + 1 < psc(outh))
        {
            int gi2 = gi + outcstep * 4;
            if (base_ch.y < nout) buffer_st1(top_blob_data_1, gi2, sum2.r);
            if (base_ch.y + 1 < nout) buffer_st1(top_blob_data_1, gi2 + outcstep, sum2.g);
            if (base_ch.y + 2 < nout) buffer_st1(top_blob_data_1, gi2 + outcstep * 2, sum2.b);
            if (base_ch.y + 3 < nout) buffer_st1(top_blob_data_1, gi2 + outcstep * 3, sum2.a);
        }
        if (gy + 1 < psc(outh) && gx + 1 < psc(outw))
        {
            int gi3 = gi + outcstep * 4 + 1;
            if (base_ch.y < nout) buffer_st1(top_blob_data_1, gi3, sum3.r);
            if (base_ch.y + 1 < nout) buffer_st1(top_blob_data_1, gi3 + outcstep, sum3.g);
            if (base_ch.y + 2 < nout) buffer_st1(top_blob_data_1, gi3 + outcstep * 2, sum3.b);
            if (base_ch.y + 3 < nout) buffer_st1(top_blob_data_1, gi3 + outcstep * 3, sum3.a);
        }
    }
}
